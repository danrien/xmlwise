apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

repositories {
    jcenter()
    mavenCentral()
}

group = "com.namehillsoftware"
archivesBaseName = "xmlwise"
version = "1.2"

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
//                name 'lazy-j'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Xmlwise'
                url 'http://www.namehillsoftware.com/'

                scm {
                    connection 'https://github.com/danrien/xmlwise.git'
                    developerConnection 'https://github.com/danrien/xmlwise.git'
                    url 'https://github.com/danrien/xmlwise'
                }

                licenses {
                    license {
//                        name 'GNU Lesser General Public License v3.0'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'david'
//                        name 'David Vedvick'
                        email 'dvedvick@gmail.com'
                    }
                }
            }
        }
    }
}